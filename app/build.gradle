apply plugin: 'com.android.application'

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (ignored) {
        return null
    }
}

def getVersionNumber = { ->
    def nome = getVersionName()
    try {
        return Integer.valueOf(nome.replace(".", ""))
    } catch (Exception ignored) {
        return 1
    }
}

android {
    signingConfigs {
        release {
            if (System.getenv("SIGNING_KEY_ALIAS")) {
                storeFile file(System.getenv("SIGNING_STORE_FILENAME"))
                keyAlias System.getenv("SIGNING_KEY_ALIAS")
                keyPassword System.getenv("SIGNING_KEY_PASSWORD")
                storePassword System.getenv("SIGNING_STORE_PASSWORD")
            }
        }
    }
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.barisi.flavio.bibbiacattolica"
        minSdkVersion 15
        targetSdkVersion 33
        versionCode getVersionNumber()
        versionName getVersionName()
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        if (System.getenv("SIGNING_KEY_ALIAS")) {
            signingConfig signingConfigs.release
        }
    }
    buildTypes {
        debug {
            shrinkResources true
            minifyEnabled true
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
    namespace 'com.barisi.flavio.bibbiacattolica'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'com.github.deano2390:MaterialShowcaseView:1.3.7'
    implementation 'com.github.AppIntro:AppIntro:6.2.0'
    implementation 'com.thebluealliance:spectrum:0.7.1'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.github.rey5137:material:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.github.chrisbanes:PhotoView:2.0.0'

    testImplementation 'junit:junit:4.12'
}
